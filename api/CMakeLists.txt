## Copyright 2018 Intel Corporation
## SPDX-License-Identifier: Apache-2.0

set(API_SOURCES
  api.cpp
)

add_library(OpenImageDenoise ${OIDN_LIB_TYPE} ${API_SOURCES} ${OIDN_RESOURCE_FILE})

if(OIDN_DEVICE_WEBGPU)
  add_subdirectory(${PROJECT_SOURCE_DIR}/devices/webgpu ${CMAKE_CURRENT_BINARY_DIR}/webgpu)
  target_sources(OpenImageDenoise PRIVATE ${OIDN_WEBGPU_SOURCE_FILES})
  target_link_libraries(OpenImageDenoise PUBLIC ${OIDN_WGPU_DIR}/lib/libwgpu_native.a)
  target_include_directories(OpenImageDenoise PUBLIC
    $<BUILD_INTERFACE:${OIDN_WGPU_DIR}/include>)
endif()

set_target_properties(OpenImageDenoise PROPERTIES
  OUTPUT_NAME ${OIDN_LIBRARY_NAME}
)
if(OIDN_LIBRARY_VERSIONED)
  set_target_properties(OpenImageDenoise PROPERTIES
    VERSION ${PROJECT_VERSION}
    SOVERSION ${PROJECT_VERSION_MAJOR}
  )
endif()

target_include_directories(OpenImageDenoise
  PUBLIC
    $<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

target_link_libraries(OpenImageDenoise PRIVATE OpenImageDenoise_core)

if(NOT OIDN_STATIC_LIB)
  oidn_strip_symbols(OpenImageDenoise)
endif()

install(TARGETS OpenImageDenoise
  EXPORT
    OpenImageDenoise_Exports
  ARCHIVE
    DESTINATION ${CMAKE_INSTALL_LIBDIR} COMPONENT devel
  LIBRARY
    DESTINATION ${CMAKE_INSTALL_LIBDIR} COMPONENT devel
  RUNTIME
    DESTINATION ${CMAKE_INSTALL_BINDIR} COMPONENT lib
)